
plugins {
    id "org.jetbrains.intellij" version "0.4.21"
    id "com.adarshr.test-logger" version "1.7.0"
    id 'idea'
    id 'java'
    id 'jacoco'
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'
def remoteRobotVersion = "0.9.35"
def fixturesVersion = "1.1.18"

intellij {
    version ideaVersion //for a full list of IntelliJ IDEA releases please see https://www.jetbrains.com/intellij-repository/releases
    if (ideaVersion.substring(3) == "2020.2") {
        plugins 'java', 'maven', 'maven-model', 'gradle', 'properties', 'yaml'
    } else if (ideaVersion.substring(3) != "2019.1") {
        plugins 'java', 'maven', 'gradle', 'properties', 'yaml'
    } else {
        plugins 'maven', 'gradle', 'properties', 'yaml'
    }
    pluginName 'Quarkus Tools'
    updateSinceUntilBuild false
}

publishPlugin {
    token    jetBrainsToken
    channels jetBrainsChannel
}

//LSP plugin causes this task to fail
buildSearchableOptions.enabled = false

repositories {
    mavenCentral()
    maven {
        url 'https://repository.jboss.org/nexus/content/repositories/snapshots'
    }
    maven {
        url 'https://repository.jboss.org/nexus/content/groups/public'
    }
    maven {
        url 'https://repo.eclipse.org/content/repositories/lsp4mp-snapshots'
    }
    maven {
        url 'https://repo.eclipse.org/content/repositories/lsp4mp-releases'
    }
    maven {
        url 'https://jetbrains.bintray.com/intellij-third-party-dependencies'
    }
}

configurations {
    compile {
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }
    lsp
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

downloadRobotServerPlugin.version = remoteRobotVersion

sourceSets {
    test {
        java.srcDir 'src/test/java'
        java.srcDir 'intellij-community/java/compiler/tests'
        java.srcDir 'intellij-community/platform/external-system-impl/testSrc'
        java.srcDir 'intellij-community/plugins/gradle/testSources'
        java.srcDir 'intellij-community/plugins/gradle/tooling-extension-impl/testSources'
        resources.srcDir file('src/test/resources')
    }

    integrationTest {
        java.srcDir file('src/it/java')
        resources.srcDir file('src/it/resources')
        compileClasspath += sourceSets.main.output + configurations.testRuntime
        runtimeClasspath += output + compileClasspath
    }
}

task integrationTest(type: Test) {
    description = 'Runs the integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
    mustRunAfter test
}

tasks.withType(Test) {
    environment 'GRADLE_RELEASE_REPOSITORY','https://services.gradle.org/distributions'
}

dependencies {
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.9.9'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.9.9.3'
    compile 'org.zeroturnaround:zt-zip:1.14'
    compile 'com.kotcrab.remark:remark:1.2.0'
    compile 'io.quarkus:quarkus-core:1.5.0.Final'
    compile 'io.quarkus:quarkus-core-deployment:1.5.0.Final'
    compile 'io.quarkus:quarkus-arc:1.5.0.Final'
    compile 'org.eclipse.lsp4mp:org.eclipse.lsp4mp.ls:0.1.0'
    compile 'org.eclipse.lsp4j:org.eclipse.lsp4j:0.8.1'
    compile 'com.vladsch.flexmark:flexmark:0.50.50'
    integrationTestImplementation("com.intellij.remoterobot:remote-robot:$remoteRobotVersion")
    integrationTestImplementation("com.intellij.remoterobot:remote-fixtures:$fixturesVersion")
    integrationTestImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
    lsp('org.eclipse.lsp4mp:org.eclipse.lsp4mp.ls:0.1.0:uber') {
        transitive = false
    }
    lsp ('com.redhat.microprofile:com.redhat.quarkus.ls:0.9.0') {
        transitive = false
    }
    compile files(new File(buildDir, 'server')) {
        builtBy 'copyDeps'
    }
}


task copyDeps(type: Copy) {
    from configurations.lsp
    into new File(buildDir, 'server/server')
}

runIdeForUiTests {
    systemProperty "robot-server.port", "8082" // default port 8080
}

prepareSandbox {
    dependsOn copyDeps
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}

check.dependsOn jacocoTestReport

integrationTest {
    useJUnitPlatform()
}

group 'com.redhat.devtools.intellij'
version projectVersion // Plugin version
