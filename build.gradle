
plugins {
    id "org.jetbrains.intellij" version "0.4.10"
}


apply plugin: 'idea'
apply plugin: 'org.jetbrains.intellij'
apply plugin: 'java'
apply plugin: 'jacoco'

intellij {
    version ideaVersion //for a full list of IntelliJ IDEA releases please see https://www.jetbrains.com/intellij-repository/releases
    plugins 'maven', 'properties', 'com.github.gtache.lsp:1.5.4'
    pluginName 'com.redhat.devtools.intellij.quarkus'
    updateSinceUntilBuild false
}

publishPlugin {
    token    jetBrainsToken
    channels jetBrainsChannel
}

//LSP plugin causes this task to fail
buildSearchableOptions.enabled = false

repositories {
    mavenCentral()
    maven {
        url 'https://repository.jboss.org/nexus/content/repositories/snapshots'
    }
}
configurations {
    compile {
        exclude group: 'org.slf4j', module: 'slf4j-api'
        exclude group: 'org.eclipse.lsp4j'
    }
    lsp
}

dependencies {
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.9.9'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.9.9.3'
    compile 'org.zeroturnaround:zt-zip:1.13'
    compile 'com.redhat.quarkus:com.redhat.quarkus.ls:0.0.2-SNAPSHOT'
    lsp 'com.redhat.quarkus:com.redhat.quarkus.ls:0.0.2-SNAPSHOT:uber'
    compile files(new File(buildDir, 'server')) {
        builtBy 'copyDeps'
    }
}


task copyDeps(type: Copy) {
    from configurations.lsp
    into new File(buildDir, 'server/server')
}

prepareSandbox {
    dependsOn copyDeps
}

group 'com.redhat.devtools.intellij'
version projectVersion // Plugin version
