
plugins {
    id "org.jetbrains.intellij" version "0.4.10"
    id "com.adarshr.test-logger" version "1.7.0"
    id 'idea'
    id 'java'
    id 'jacoco'
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

intellij {
    version ideaVersion //for a full list of IntelliJ IDEA releases please see https://www.jetbrains.com/intellij-repository/releases
    if (ideaVersion.substring(3) == "2020.2") {
        plugins 'java', 'maven', 'maven-model', 'gradle', 'properties', 'yaml'
    } else if (ideaVersion.substring(3) != "2019.1") {
        plugins 'java', 'maven', 'gradle', 'properties', 'yaml'
    } else {
        plugins 'maven', 'gradle', 'properties', 'yaml'
    }
    pluginName 'Quarkus Tools'
    updateSinceUntilBuild false
}

publishPlugin {
    token    jetBrainsToken
    channels jetBrainsChannel
}

//LSP plugin causes this task to fail
buildSearchableOptions.enabled = false

repositories {
    mavenCentral()
    maven {
        url 'https://repository.jboss.org/nexus/content/repositories/snapshots'
    }
    maven {
        url 'https://repo.eclipse.org/content/repositories/lsp4mp-snapshots'
    }
}

configurations {
    compile {
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }
    lsp
}

sourceSets {
    test {
        java.srcDir 'src/test/java'
        java.srcDir 'intellij-community/java/compiler/tests'
        java.srcDir 'intellij-community/platform/external-system-impl/testSrc'
        java.srcDir 'intellij-community/plugins/gradle/testSources'
        java.srcDir 'intellij-community/plugins/gradle/tooling-extension-impl/testSources'
        resources.srcDir file('src/test/resources')
    }
}

tasks.withType(Test) {
    environment 'GRADLE_RELEASE_REPOSITORY','https://services.gradle.org/distributions'
}

dependencies {
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.9.9'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.9.9.3'
    compile 'org.zeroturnaround:zt-zip:1.14'
    compile 'com.kotcrab.remark:remark:1.2.0'
    compile 'io.quarkus:quarkus-core:1.5.0.Final'
    compile 'io.quarkus:quarkus-core-deployment:1.5.0.Final'
    compile 'io.quarkus:quarkus-arc:1.5.0.Final'
    compile 'org.eclipse.lsp4mp:org.eclipse.lsp4mp.ls:0.1.0-SNAPSHOT'
    compile 'org.eclipse.lsp4j:org.eclipse.lsp4j:0.8.1'
    compile 'com.vladsch.flexmark:flexmark:0.50.50'
    lsp('org.eclipse.lsp4mp:org.eclipse.lsp4mp.ls:0.1.0-SNAPSHOT:uber') {
        transitive = false
    }
    lsp ('com.redhat.microprofile:com.redhat.quarkus.ls:0.9.0-SNAPSHOT') {
        transitive = false
    }
    compile files(new File(buildDir, 'server')) {
        builtBy 'copyDeps'
    }
}


task copyDeps(type: Copy) {
    from configurations.lsp
    into new File(buildDir, 'server/server')
}

prepareSandbox {
    dependsOn copyDeps
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}

check.dependsOn jacocoTestReport

group 'com.redhat.devtools.intellij'
version projectVersion // Plugin version
